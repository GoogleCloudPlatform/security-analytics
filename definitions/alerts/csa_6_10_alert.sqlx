/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
  type: "view",
  description: "connection_from_new_IP",
  tags: ["alert"],
}

SELECT
  *
FROM (
  WITH actions AS (
    SELECT
      JSON_VALUE(json_payload.connection.src_ip) as src_ip,
      -- TIMESTAMP supports up to 6 digits of fractional precision, so drop any more digits to avoid parse errors
      MIN(TIMESTAMP(REGEXP_REPLACE(JSON_VALUE(json_payload.start_time), r'\.(\d{0,6})\d+(Z)?$', '.\\1\\2'))) AS first_seen,
      MAX(TIMESTAMP(REGEXP_REPLACE(JSON_VALUE(json_payload.start_time), r'\.(\d{0,6})\d+(Z)?$', '.\\1\\2'))) AS last_seen,
      ARRAY_AGG(DISTINCT JSON_VALUE(resource.labels.subnetwork_name)) as subnetwork_names,
      ARRAY_AGG(DISTINCT JSON_VALUE(json_payload.dest_instance.vm_name)) as vm_names,
      COUNT(*) counter
    FROM ${ref("_AllLogs")}
    WHERE
      timestamp >=  TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL ${dataform.projectConfig.vars.alert_interval_minutes} MINUTE)
      AND JSON_VALUE(json_payload.reporter) = 'DEST'
      -- AND JSON_VALUE(resource.labels.subnetwork_name) IN ('prod-customer-data')
    GROUP BY
      src_ip
    ORDER BY
      last_seen DESC
  )
  SELECT
    st.* EXCEPT(first_seen, last_seen),
    -- flag any new never-seen-before IP
    lt.first_seen IS NULL AS is_new,
    -- retrieve all-time first instance from historical lookup table
    IF(lt.first_seen IS NULL, st.first_seen, lt.first_seen) AS first_seen,
    -- retrieve all-time last instance from current summary table
    st.last_seen
  FROM actions st
  LEFT JOIN ${ref("csa_6_10_ips_lookup")} lt USING(src_ip)
)
WHERE is_new
