config {
  type: "view",
  description: "hosts_reaching_to_many_other_hosts_or_ports"
}


SELECT
  TIMESTAMP_TRUNC(TIMESTAMP(REGEXP_REPLACE(JSON_VALUE(json_payload.start_time), r'\.(\d{0,6})\d+(Z)?$', '.\\1\\2')), HOUR) as time_window,
  JSON_VALUE(json_payload.connection.src_ip) AS src_ip,
  COUNT(DISTINCT JSON_VALUE(json_payload.connection.dest_ip)) AS numDestIps,
  COUNT(DISTINCT JSON_VALUE(json_payload.connection.dest_port)) AS numDestPorts,
  ARRAY_AGG(DISTINCT JSON_VALUE(resource.labels.subnetwork_name)) AS subnetNames,
  ARRAY_AGG(DISTINCT IF(JSON_VALUE(json_payload.reporter) = "DEST", JSON_VALUE(json_payload.dest_instance.vm_name), JSON_VALUE(json_payload.src_instance.vm_name))) as VMs,
  COUNT(*) numSamples
FROM
  ${ref("_AllLogs")}
WHERE
  timestamp >=  TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL ${dataform.projectConfig.vars.raw_lookback_days} DAY)
  AND log_id = "compute.googleapis.com/vpc_flows"
GROUP BY
  time_window,
  src_ip
HAVING
  numDestIps > 10
  OR numDestPorts > 10
ORDER BY
   time_window DESC
   