config {
  type: "view",
  description: "recent_dataset_activity_with_granted_permissions"
}


SELECT
  timestamp,
  REGEXP_EXTRACT(proto_payload.audit_log.resource_name, 'projects/([^/]+)') as projectid,
  REGEXP_EXTRACT(proto_payload.audit_log.resource_name, '/datasets/([^/]+)') AS datasetid,
  proto_payload.audit_log.authentication_info.principal_email as principalemail,
  proto_payload.audit_log.request_metadata.caller_ip as callerip,
  auth.permission as permission,
  proto_payload.audit_log.request_metadata.caller_supplied_user_agent as agent,
  proto_payload.audit_log.method_name as method,
  proto_payload.audit_log.status.message as status,
  auth.granted as granted
FROM ${ref("_AllLogs")},
  UNNEST(proto_payload.audit_log.authorization_info) as auth
WHERE
  log_id="cloudaudit.googleapis.com/activity"
  AND LOWER(proto_payload.audit_log.method_name) like '%dataset%'
  AND timestamp >=  TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL ${dataform.projectConfig.vars.raw_lookback_days} DAY)
