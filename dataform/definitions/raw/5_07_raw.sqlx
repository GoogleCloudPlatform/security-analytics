config {
  type: "view",
  description: "BQ_queries_with_large_scans",
  disabled: true
}


SELECT
  timestamp,
  proto_payload.audit_log.authentication_info.principal_email as principal_email,
  proto_payload.audit_log.request_metadata.caller_ip,
  proto_payload.audit_log.request_metadata.caller_supplied_user_agent,
  CAST(JSON_VALUE(proto_payload.audit_log.metadata, "$.jobChange.job.jobStats.queryStats.totalBilledBytes") AS INT64) AS totalBilledBytes,
  JSON_VALUE(proto_payload.audit_log.metadata, "$.jobChange.job.jobConfig.queryConfig.query") AS query
FROM
  ${ref("_AllLogs")}
WHERE
  timestamp >=  TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL ${dataform.projectConfig.vars.raw_lookback_days} DAY)
  AND log_id="cloudaudit.googleapis.com/data_access"
  AND operation.last = TRUE 
  AND STARTS_WITH(resource.type, 'bigquery') IS TRUE
  AND JSON_VALUE(proto_payload.audit_log.metadata, "$.jobChange.job.jobConfig.type") = "QUERY"
  AND JSON_VALUE(proto_payload.audit_log.metadata, "$.jobChange.job.jobConfig.queryConfig.statementType") = "SELECT"
  AND CAST(JSON_VALUE(proto_payload.audit_log.metadata, "$.jobChange.job.jobStats.queryStats.totalBilledBytes") AS INT64) > 1073741824
ORDER BY
  timestamp DESC
