/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
  type: "incremental",
  uniqueKey: ["day", "user", "user_agent", "ip"],
  bigquery: {
    partitionBy: "day",
    updatePartitionFilter:
      "day >= DATE_SUB(CURRENT_DATE(), INTERVAL 1 DAY)",
    clusterBy: ["user"]
  }
}

pre_operations {
  DECLARE time_window_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT CAST(MAX(day) AS TIMESTAMP) FROM ${self()}`,
    `SELECT TIMESTAMP("2000-01-01")`)}
  )
}

SELECT
  EXTRACT(DATE FROM timestamp) AS day,
  proto_payload.audit_log.authentication_info.principal_email as user,
  proto_payload.audit_log.request_metadata.caller_supplied_user_agent as user_agent,
  proto_payload.audit_log.request_metadata.caller_ip as ip,
  MAX(timestamp) as last_seen,
  MIN(timestamp) as first_seen,
  CASE
    WHEN proto_payload.audit_log.request_metadata.caller_supplied_user_agent LIKE "Mozilla/%" THEN 'Cloud Console'
    WHEN proto_payload.audit_log.request_metadata.caller_supplied_user_agent LIKE "google-cloud-sdk gcloud/%" THEN 'gcloud CLI'
    WHEN proto_payload.audit_log.request_metadata.caller_supplied_user_agent LIKE "google-api-go-client/% Terraform/%" THEN 'Terraform'
    ELSE 'Other'
  END AS channel,
FROM ${ref("_AllLogs")}
WHERE
  ${ when(incremental(), `timestamp >= time_window_checkpoint AND`) }
  timestamp >=  TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL ${dataform.projectConfig.vars.summary_lookback_days} DAY)
  AND log_id = "cloudaudit.googleapis.com/data_access"
  AND proto_payload.audit_log.service_name = "cloudresourcemanager.googleapis.com"
  AND proto_payload.audit_log.method_name IN ("GetProject", "FindOrCreateOrganization")
GROUP BY
  day, user, user_agent, ip