/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
  type: "incremental",
  tags: ["summary", "daily"],
  uniqueKey: ["day", "principal_email"],
  bigquery: {
    partitionBy: "day",
    updatePartitionFilter:
      "day >= DATE_SUB(CURRENT_DATE(), INTERVAL 1 DAY)",
    clusterBy: ["principal_email"]
  }
}

pre_operations {
  DECLARE time_window_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT CAST(MAX(day) AS TIMESTAMP) FROM ${self()}`,
    `SELECT TIMESTAMP("2000-01-01")`)}
  )
}

SELECT
  EXTRACT(DATE FROM timestamp) AS day,
  proto_payload.audit_log.authentication_info.principal_email,
  MIN(timestamp) AS first_seen,
  MAX(timestamp) AS last_seen,
  ARRAY_AGG(DISTINCT proto_payload.audit_log.method_name IGNORE NULLS) as method_names,
  ARRAY_AGG(DISTINCT COALESCE(proto_payload.audit_log.resource_name, 'ALL')) as resource_names,
  ARRAY_AGG(DISTINCT proto_payload.audit_log.request_metadata.caller_supplied_user_agent IGNORE NULLS) as user_agents,
  COUNT(*) counter
FROM ${ref("_AllLogs")}
WHERE
  ${ when(incremental(), `timestamp >= time_window_checkpoint AND`) }
  timestamp >=  TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL ${dataform.projectConfig.vars.summary_lookback_days} DAY)
  AND proto_payload.audit_log.service_name = 'storage.googleapis.com'
  AND proto_payload.audit_log.method_name LIKE 'storage.%.list'
  AND proto_payload.audit_log.authentication_info.principal_email NOT IN (
    -- Actor exclusions
    "service-account-123456@developer.gserviceaccount.com",
    "user@example.com"
  )
  AND (proto_payload.audit_log.resource_name NOT IN (
    -- Resource (bucket) exclusions
    "projects/_/buckets/non-sensitive-bucket"
  ) OR proto_payload.audit_log.resource_name IS NULL)
GROUP BY
  day, principal_email