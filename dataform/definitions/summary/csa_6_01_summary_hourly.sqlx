/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
  type: "incremental",
  tags: ["summary", "hourly"],
  uniqueKey: ["time_window", "src_ip"],
  bigquery: {
    partitionBy: "DATE(time_window)",
    updatePartitionFilter:
        "time_window >= TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL 1 HOUR)"
  }
}

pre_operations {
  DECLARE time_window_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT MAX(time_window) FROM ${self()}`,
    `SELECT TIMESTAMP("2000-01-01")`)}
  )
}

SELECT
  TIMESTAMP_TRUNC(TIMESTAMP(REGEXP_REPLACE(JSON_VALUE(json_payload.start_time), r'\.(\d{0,6})\d+(Z)?$', '.\\1\\2')), HOUR) as time_window,
  JSON_VALUE(json_payload.connection.src_ip) AS src_ip,
  COUNT(DISTINCT JSON_VALUE(json_payload.connection.dest_ip)) AS numDestIps,
  COUNT(DISTINCT JSON_VALUE(json_payload.connection.dest_port)) AS numDestPorts,
  ARRAY_AGG(DISTINCT JSON_VALUE(resource.labels.subnetwork_name)) AS subnetNames,
  ARRAY_AGG(DISTINCT IF(JSON_VALUE(json_payload.reporter) = "DEST", JSON_VALUE(json_payload.dest_instance.vm_name), JSON_VALUE(json_payload.src_instance.vm_name)) IGNORE NULLS) as VMs,
  COUNT(*) numSamples
FROM
  ${ref("_AllLogs")}
WHERE
  ${ when(incremental(), `timestamp >= time_window_checkpoint AND`) }
  timestamp >=  TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL ${dataform.projectConfig.vars.summary_lookback_days} DAY)
  AND log_id = "compute.googleapis.com/vpc_flows"
GROUP BY
  time_window,
  src_ip
